/**
 * Design: fmc_module.phdl
 * Adapted from PADS fmc_module.prj
 * Submitted by: Pete Dudley, Sandia Labs
 * Translated by: Brad Riching, Brigham Young University
 *
 * Dependencies: none (fully contained design in this file)
 *
 * Notes:
 * This is an exact translation of the design to phdl.  Pay particular attention
 * to how easy it is to instance an array of devices (such as the sma connectors
 * and associated coupling capacitors on the receiver ports.)  All refDes constraints
 * were constrained manually to duplicate the exact reference in the schematic.  
 * However, the phdl compiler can also generate them automatically if they aren't 
 * formally constrained.
 */

// the design declaration
design fmc_module is

	/****************************************************************************************
	 * Beginning of schematic page 2 from original source
	 */

	// an SMA connector device declaration
	device SMA_CON is
		attr refprefix = "P";
		attr refDes = "";
		attr name = "";
		attr value = "";
		attr pkg_type = "SMA_STRAIGHT";
		attr mfgr = "MOLEX";
		attr attpartNumber = "73391-0070";
		attr cost = "4.97";

		pin sig = {1};
		pin[3:0] gnd = {5,4,3,2}; // gnd(3) maps to "5", gnd(2) maps to "4", gnd(1) maps to "3", gnd(0) maps to "2"
	end device;
	
	// a non-polarized surface mount capacitor device declaration
	device cap_0402 is
		attr refPrefix = "C";
		attr refDes = "";
		attr name = "";
		attr value = "";
		attr pkg_type = "CC0402";
		attr mfgr = "PANASONIC";
		attr partNumber = "ECJ-0EB1A104K";
		attr cost = "0.03";

		pin a = {1};
		pin b = {2};
	end device;
	
	// Note: These transceiver port net names could be optimized with the language.
	// For example, these four declarations would cover the next 36 declarations 
	// with a simple rearrangement of the index
	// net DP_C2M_P[0:5];
	// net DP_C2M_N[0:5];
	// net DP_M2C_P[0:5];
	// net DP_M2C_N[0:5];
	// net SMA_DP_M2C_P[0:5];
	// net SMA_DP_M2C_N[0:5];
	
	//port0 transciever nets
	net DP0_C2M_P;
	net DP0_C2M_N;
	net DP0_M2C_P;
	net DP0_M2C_N;
	net SMA_DPO_M2C_P;
	net SMA_DPO_M2C_N;
	
	//port1 transciever nets
	net DP1_C2M_P;
	net DP1_C2M_N;
	net DP1_M2C_P;
	net DP1_M2C_N;
	net SMA_DP1_M2C_P;
	net SMA_DP1_M2C_N;
	
	//port2 transciever nets
	net DP2_C2M_P;
	net DP2_C2M_N;
	net DP2_M2C_P;
	net DP2_M2C_N;
	net SMA_DP2_M2C_P;
	net SMA_DP2_M2C_N;
	
	//port3 transciever nets
	net DP3_C2M_P;
	net DP3_C2M_N;
	net DP3_M2C_P;
	net DP3_M2C_N;
	net SMA_DP3_M2C_P;
	net SMA_DP3_M2C_N;
	
	//port4 transciever nets
	net DP4_C2M_P;
	net DP4_C2M_N;
	net DP4_M2C_P;
	net DP4_M2C_N;
	net SMA_DP4_M2C_P;
	net SMA_DP4_M2C_N;
	
	//port5 transciever nets
	net DP5_C2M_P;
	net DP5_C2M_N;
	net DP5_M2C_P;
	net DP5_M2C_N;
	net SMA_DP5_M2C_P;
	net SMA_DP5_M2C_N;
	
	/****************************************************************************************
	 * End of schematic page 2, beginning of schematic page 3 from original source
	 */
	
	// an isolation transformer declaration
	device TC4-14+ is
		attr refPrefix = "T";
		attr refDes = "";
		attr name = "";
		attr value = "";
		attr pkg_type = "AT224-1";
		attr mfgr = "MINI-CIRCUITS";
		attr partNumber = "TC4-14G2+";
		attr cost = "$10.00";

		pin pri_p = {4};
		pin pri_n = {5};
		pin sec_p = {1};
		pin sec_tap = {2};
		pin sec_n = {3};
	end device;
	
	// a surface mount resistor declaration
	device res_0603 is
		attr refPrefix = "R";
		attr refDes = "";
		attr name = "";
		attr value = "";
		attr pkg_type = "CR0603";
		attr mfgr = "PANASONIC";
		attr partNumber = "ERJ-3EKF1500V";
		attr cost = "0.07";

		pin a = {1};
		pin b = {2};
	end device;
	
	// clock nets
	net CLK250P;
	net CLK250N;
	net CLK250;
	net DIFF_CLK_RTN;
	net SINGLE_CLK_RTN;
	net CLK250N_BAL;
	net CLK250P_BAL;
	net CLK250_BAL_N;
	net CLK250_BAL_P;
	
	// misc nets
	net $3N205;
	net $3N212;
	net $3N213;
	net $3N195;
	net $3N1;
	net $3N193;
	net $3N222;
	net $3N42;
	net $3N44;
	net $3N163;
	net $3N164;
	net $4N126;
	
	// power supply
	net +3V3;
	net gnd;
	
	// a tantalum capacitor declaration
	device tant_cap is
		attr refPrefix = "C";
		attr refDes = "";
		attr name = "";
		attr value = "";
		attr pkg_type = "7343-31";
		attr mfgr = "AVX CORPORATION";
		attr partNumber = "TAJD227M006RNJ";
		attr cost = "2.60";

		pin pos = {1};
		pin neg = {2};
	end device;
	
	// an clock buffer IC
	device sy898533l is
		attr refPrefix = "U";
		attr refDes = "";
		attr name = "";
		attr value = "";
		attr pkg_type = "SY898533LKZ";
		attr mfgr = "MICREL";
		attr partNumber = "SY898533LKZ";
		attr cost = "4.45";

		pin[1:3] vcc = {10,13,18};
		pin clk_p = {4};
		pin clk_n = {5};
		pin pclk_p = {6};
		pin pclk_n = {7};
		pin clk_en = {2};
		pin clk_sel = {3};
		pin[1:2] nc = {8,9};
		pin q0_p = {20};
		pin q0_n = {19};
		pin q1_p = {17};
		pin q1_n = {16};
		pin q2_p = {15};
		pin q2_n = {14};
		pin q3_p = {12};
		pin q3_n = {11};
		pin vee = {1};
	end device;
	
	// gigabit transceiver clock nets
	net GBTCLK0_M2C_P;
	net GBTCLK0_M2C_N;
	net GBTCLK1_M2C_P;
	net GBTCLK1_M2C_N;
	
	// a slide switch declaration
	device spdt2_dip is
		attr refPrefix = "P";
		attr refDes = "";
		attr name = "";
		attr value = "";
		attr pkg_type = "CAS-220B";
		attr mfgr = "COPAL ELECTRONICS";
		attr partNumber = "CAS-D20TB";
		attr cost = "2.28";

		// circuit 1
		pin p1_1 = {1}; // position1
		pin p2_1 = {3}; // position2
		pin s_1 = {2}; // select
		
		// circuit 2
		pin p1_2 = {4}; // position1
		pin p2_2 = {6}; // position2
		pin s_2 = {5}; // select
	end device;
	
	// nets for sync connectors signals
	net SYNC_OUT;
	net SYNC_IN;
	
	/****************************************************************************************
	 * End of schematic page 3, beginning of schematic page 4-5 from original source
	 */
	
	// a massive surface mount connector array
	device asp_134488_01 is
		attr refPrefix = "P";
		attr refDes = "";
		attr name = "";
		attr value = "";
		attr pkg_type = "ASP-134488-01";
		attr mfgr = "SAMTEC";
		attr partNumber = "ASP-134488-01";
		attr cost = "XXXX";

		pin[1:40] a = {a1,a2,a3,a4,a5,a6,a7,a8,a9,a10,a11,a12,a13,a14,a15,a16,a17,a18,a19,a20,
		a21,a22,a23,a24,a25,a26,a27,a28,a29,a30,a31,a32,a33,a34,a35,a36,a37,a38,a39,a40};
		pin[1:40] b = {b1,b2,b3,b4,b5,b6,b7,b8,b9,b10,b11,b12,b13,b14,b15,b16,b17,b18,b19,b20,
		b21,b22,b23,b24,b25,b26,b27,b28,b29,b30,b31,b32,b33,b34,b35,b36,b37,b38,b39,b40};
		pin[1:40] c = {c1,c2,c3,c4,c5,c6,c7,c8,c9,c10,c11,c12,c13,c14,c15,c16,c17,c18,c19,c20,
		c21,c22,c23,c24,c25,c26,c27,c28,c29,c30,c31,c32,c33,c34,c35,c36,c37,c38,c39,c40};
		pin[1:40] d = {d1,d2,d3,d4,d5,d6,d7,d8,d9,d10,d11,d12,d13,d14,d15,d16,d17,d18,d19,d20,
		d21,d22,d23,d24,d25,d26,d27,d28,d29,d30,d31,d32,d33,d34,d35,d36,d37,d38,d39,d40};
		pin[1:40] e = {e1,e2,e3,e4,e5,e6,e7,e8,e9,e10,e11,e12,e13,e14,e15,e16,e17,e18,e19,e20,
		e21,e22,e23,e24,e25,e26,e27,e28,e29,e30,e31,e32,e33,e34,e35,e36,e37,e38,e39,e40};
		pin[1:40] f = {f1,f2,f3,f4,f5,f6,f7,f8,f9,f10,f11,f12,f13,f14,f15,f16,f17,f18,f19,f20,
		f21,f22,f23,f24,f25,f26,f27,f28,f29,f30,f31,f32,f33,f34,f35,f36,f37,f38,f39,f40};
		pin[1:40] g = {g1,g2,g3,g4,g5,g6,g7,g8,g9,g10,g11,g12,g13,g14,g15,g16,g17,g18,g19,g20,
		g21,g22,g23,g24,g25,g26,g27,g28,g29,g30,g31,g32,g33,g34,g35,g36,g37,g38,g39,g40};
		pin[1:40] h = {h1,h2,h3,h4,h5,h6,h7,h8,h9,h10,h11,h12,h13,h14,h15,h16,h17,h18,h19,h20,
		h21,h22,h23,h24,h25,h26,h27,h28,h29,h30,h31,h32,h33,h34,h35,h36,h37,h38,h39,h40};
		pin[1:40] i = {i1,i2,i3,i4,i5,i6,i7,i8,i9,i10,i11,i12,i13,i14,i15,i16,i17,i18,i19,i20,
		i21,i22,i23,i24,i25,i26,i27,i28,i29,i30,i31,i32,i33,i34,i35,i36,i37,i38,i39,i40};
		pin[1:40] j = {j1,j2,j3,j4,j5,j6,j7,j8,j9,j10,j11,j12,j13,j14,j15,j16,j17,j18,j19,j20,
		j21,j22,j23,j24,j25,j26,j27,j28,j29,j30,j31,j32,j33,j34,j35,j36,j37,j38,j39,j40};
		pin[1:40] k = {k1,k2,k3,k4,k5,k6,k7,k8,k9,k10,k11,k12,k13,k14,k15,k16,k17,k18,k19,k20,
		k21,k22,k23,k24,k25,k26,k27,k28,k29,k30,k31,k32,k33,k34,k35,k36,k37,k38,k39,k40};
	end device;
	
	// a pin header declaration
	device hdr_2x8 is
		attr refPrefix = "J";
		attr refDes = "";
		attr name = "";
		attr value = "";
		attr pkg_type = "HDR2x8/0_100";
		attr mfgr = "MOLEX";
		attr partNumber = "90131-0768";
		attr cost = "1.03";

		pin[0:7] sig = {1,3,5,7,9,11,13,15};
		pin[0:7] gnd = {2,4,6,8,10,12,14,16};
	end device;
	
	net[7:0] DISCRETE;

begin
	
	/**
	 * Beginning of schematic page 2 from original source
	 */
	
	// an array of four sma connectors for port0
	inst(1:4) port0 of SMA_CON is
		// refDes constraints defined in the original source
		port0(1).refDes = "P9";
		port0(2).refDes = "P10";
		port0(3).refDes = "P16";
		port0(4).refDes = "P15";

		port0(1).sig = DP0_C2M_P;
		port0(2).sig = DP0_C2M_N;
		port0(3).sig = SMA_DPO_M2C_P;
		port0(4).sig = SMA_DPO_M2C_N;
	
		// all ground pins of all instances are connected to ground with this one line.
		gnd = <gnd>;
		
		/** This last line could optional be written: gnd[1:4][3:0] = gnd;
		 * where the first array references all instances of the pin,
		 * and the second array references all pins in each instance.
		 */
	end inst;
	
	inst(1:4) port1 of SMA_CON is
		port1(1).refDes = "P12";
		port1(2).refDes = "P11";
		port1(3).refDes = "P13";
		port1(4).refDes = "P14";
		
		port1(1).sig = DP1_C2M_P;
		port1(2).sig = DP1_C2M_N;
		port1(3).sig = SMA_DP1_M2C_P;
		port1(4).sig = SMA_DP1_M2C_N;
		gnd = <gnd>;
	end inst;
	
	inst(1:4) port2 of SMA_CON is
		port2(1).refDes = "P19";
		port2(2).refDes = "P20";
		port2(3).refDes = "P18";
		port2(4).refDes = "P17";

		port2(1).sig = DP1_C2M_P;
		port2(2).sig = DP1_C2M_N;
		port2(3).sig = SMA_DP1_M2C_P;
		port2(4).sig = SMA_DP1_M2C_N;
		gnd = <gnd>;
	end inst;
	
	inst(1:4) port3 of SMA_CON is
		port3(1).refDes = "P22";
		port3(2).refDes = "P21";
		port3(3).refDes = "P23";
		port3(4).refDes = "P24";

		port3(1).sig = DP1_C2M_P;
		port3(2).sig = DP1_C2M_N;
		port3(3).sig = SMA_DP1_M2C_P;
		port3(4).sig = SMA_DP1_M2C_N;
		gnd = <gnd>;
	end inst;
	
	inst(1:4) port4 of SMA_CON is
		port4(1).refDes = "P3";
		port4(2).refDes = "P4";
		port4(3).refDes = "P2";
		port4(4).refDes = "P1";

		port4(1).sig = DP1_C2M_P;
		port4(2).sig = DP1_C2M_N;
		port4(3).sig = SMA_DP1_M2C_P;
		port4(4).sig = SMA_DP1_M2C_N;
		gnd = <gnd>;
	end inst;
	
	inst(1:4) port5 of SMA_CON is
		port5(1).refDes = "P6";
		port5(2).refDes = "P5";
		port5(3).refDes = "P7";
		port5(4).refDes = "P8";

		port5(1).sig = DP1_C2M_P;
		port5(2).sig = DP1_C2M_N;
		port5(3).sig = SMA_DP1_M2C_P;
		port5(4).sig = SMA_DP1_M2C_N;
		gnd = <gnd>;
	end inst;
	
	// all receiver coupling capacitors for all sma ports
	inst(1:12) rx_coupling_caps of cap_0402 is
		// all values are 1uF.
		value = "1uF";

		// refDes constraints as defined in the original source 
		rx_coupling_caps(1).refDes = "C9";
		rx_coupling_caps(2).refDes = "C10";
		rx_coupling_caps(3).refDes = "C11";
		rx_coupling_caps(4).refDes = "C12";
		rx_coupling_caps(5).refDes = "C14";
		rx_coupling_caps(6).refDes = "C13";
		rx_coupling_caps(7).refDes = "C15";
		rx_coupling_caps(8).refDes = "C4";
		rx_coupling_caps(9).refDes = "C6";
		rx_coupling_caps(10).refDes = "C5";
		rx_coupling_caps(11).refDes = "C7";
		rx_coupling_caps(12).refDes = "C8";

		// all coupling caps a-side
		rx_coupling_caps(1).a = SMA_DPO_M2C_P;
		rx_coupling_caps(2).a = SMA_DPO_M2C_N;
		rx_coupling_caps(3).a = SMA_DP1_M2C_P;
		rx_coupling_caps(4).a = SMA_DP1_M2C_N;
		rx_coupling_caps(5).a = SMA_DP2_M2C_P;
		rx_coupling_caps(6).a = SMA_DP2_M2C_N;
		rx_coupling_caps(7).a = SMA_DP3_M2C_P;
		rx_coupling_caps(8).a = SMA_DP3_M2C_N;
		rx_coupling_caps(9).a = SMA_DP4_M2C_P;
		rx_coupling_caps(10).a = SMA_DP4_M2C_N;
		rx_coupling_caps(11).a = SMA_DP5_M2C_P;
		rx_coupling_caps(12).a = SMA_DP5_M2C_N;
		// all coupling caps b-side
		rx_coupling_caps(1).b = DP0_M2C_P;
		rx_coupling_caps(2).b = DP0_M2C_N;
		rx_coupling_caps(3).b = DP1_M2C_P;
		rx_coupling_caps(4).b = DP1_M2C_N;
		rx_coupling_caps(5).b = DP2_M2C_P;
		rx_coupling_caps(6).b = DP2_M2C_N;
		rx_coupling_caps(7).b = DP3_M2C_P;
		rx_coupling_caps(8).b = DP3_M2C_N;
		rx_coupling_caps(9).b = DP4_M2C_P;
		rx_coupling_caps(10).b = DP4_M2C_N;
		rx_coupling_caps(11).b = DP5_M2C_P;
		rx_coupling_caps(12).b = DP5_M2C_N;
	end inst;
	
	/****************************************************************************************
	 * End of schematic page 2, beginning of schematic page 3 from original source
	 */
	 
	 // the differential clock connectors
	inst(1:2) diff_clk_input of SMA_CON is
		diff_clk_input(1).refDes = "P30";
		diff_clk_input(2).refDes = "P26";

		diff_clk_input(1).sig = CLK250P;
		diff_clk_input(2).sig = CLK250N;
		gnd = <DIFF_CLK_RTN>;
	end inst;
	
	// the single ended clock connector
	inst single_ended_clk_input of SMA_CON is
		refDes = "P25";

		sig = CLK250;
		gnd = <SINGLE_CLK_RTN>;
	end inst;
	
	inst diff_iso_transformer of TC4-14+ is
		refDes = "T2";

		pri_p = CLK250P;
		pri_n = CLK250N;
		sec_p = $3N212;
		sec_tap = $3N205;
		sec_n = $3N213;
	end inst;
	
	inst single_iso_transformer of TC4-14+ is
		refDes = "T1";

		pri_p = SINGLE_CLK_RTN;
		pri_n = CLK250;
		sec_p = $3N195;
		sec_tap = $3N1;
		sec_n = $3N193;
	end inst;
	
	// the clock impedance matching-biasing resitor network
	inst(1:10) clk_biasing_network of res_0603 is
		clk_biasing_network(1:2).value = "100";
		clk_biasing_network(3:4).value = "49.9";
		clk_biasing_network(5:8).value = "150";
		clk_biasing_network(9).value = "200";
		clk_biasing_network(10).value = "100";
		
		// constrained refdes's
		clk_biasing_network(1).refDes = "R10";
		clk_biasing_network(2).refDes = "R12";
		clk_biasing_network(3).refDes = "R3";
		clk_biasing_network(4).refDes = "R7";
		clk_biasing_network(5).refDes = "R11";
		clk_biasing_network(6).refDes = "R15";
		clk_biasing_network(7).refDes = "R13";
		clk_biasing_network(8).refDes = "R14";
		clk_biasing_network(9).refDes = "R8";
		clk_biasing_network(10).refDes = "R4";
		
		// impedance matching
		clk_biasing_network(1).a = CLK250N_BAL;
		clk_biasing_network(1).b = $3N212;
		clk_biasing_network(2).a = CLK250P_BAL;
		clk_biasing_network(2).b = $3N213;
		clk_biasing_network(3).a = CLK250_BAL_N;
		clk_biasing_network(3).b = $3N193;
		clk_biasing_network(4).a = CLK250_BAL_P;
		clk_biasing_network(4).b = $3N195;
		
		// biasing
		clk_biasing_network(5:6).a = +3V3;
		clk_biasing_network(5).b = $3N205;
		clk_biasing_network(6).b = $3N1;
		clk_biasing_network(7).a = $3N205;
		clk_biasing_network(8).a = $3N1;
		clk_biasing_network(7:8).b = gnd;
		
		// terminating
		clk_biasing_network(9).a = CLK250N_BAL;
		clk_biasing_network(9).b = CLK250P_BAL;
		clk_biasing_network(10).a = CLK250_BAL_N;
		clk_biasing_network(10).b = CLK250_BAL_P;
	end inst;
	
	// all of the power supply's decoupling capacitors
	inst(1:10) decoupling_caps of tant_cap is
		decoupling_caps(1:3).value = "220uF";
		decoupling_caps(4:10).value = "0.1uF";
		decoupling_caps(1).refDes = "C24";
		decoupling_caps(2).refDes = "C25";
		decoupling_caps(3).refDes = "C26";
		decoupling_caps(4).refDes = "C20";
		decoupling_caps(5).refDes = "C21";
		decoupling_caps(6).refDes = "C22";
		decoupling_caps(7).refDes = "C23";
		decoupling_caps(8).refDes = "C1";
		decoupling_caps(9).refDes = "C2";
		decoupling_caps(10).refDes = "C3";

		pos = <+3V3>;
		neg = <gnd>;
	end inst;
	
	// the instantiated clock buffer IC
	inst clock_buffer of sy898533l is
		refDes = "U1";

		vcc = <+3V3>;
		clk_p = CLK250P_BAL;
		clk_n = CLK250N_BAL;
		pclk_p = CLK250_BAL_P;
		pclk_n = CLK250_BAL_N;
		clk_en = +3V3;
		clk_sel = $3N222;
		nc = open;
		q0_p = $3N42;
		q0_n = $3N44;
		q1_p = $3N163;
		q1_n = $3N164;
		q2_p = open;
		q2_n = open;
		q3_p = open;
		q3_n = open;
		vee = gnd;
	end inst;
	
	// clock buffer output isolation capacitors
	inst(1:4) clk_buffer_iso_caps of cap_0402 is
		value = "1uF";

		clk_buffer_iso_caps(1).a = $3N42;
		clk_buffer_iso_caps(2).a = $3N44;
		clk_buffer_iso_caps(3).a = $3N163;
		clk_buffer_iso_caps(4).a = $3N164;
		clk_buffer_iso_caps(1).b = GBTCLK0_M2C_P;
		clk_buffer_iso_caps(2).b = GBTCLK0_M2C_N;
		clk_buffer_iso_caps(3).b = GBTCLK1_M2C_P;
		clk_buffer_iso_caps(4).b = GBTCLK1_M2C_P;
	end inst;
	
	// clock buffer output terminations
	inst(1:4) clk_buffer_terminations of res_0603 is
		value = "120";

		clk_buffer_terminations(1).a = $3N42;
		clk_buffer_terminations(2).a = $3N44;
		clk_buffer_terminations(3).a = $3N163;
		clk_buffer_terminations(4).a = $3N164;
		b = <gnd>;
	end inst;
	
	// the differential or single ended clock input select switch
	inst diff_single_select of spdt2_dip is
		refDes = "P29";

		// circuit 1
		p1_1 = +3V3;
		p2_1 = gnd;
		s_1 = $3N222;
		
		// circuit 2 (not used)
		p1_2 = +3V3;
		p2_2 = gnd;
		s_2 = open;
	end inst;
	
	// the synchronization connectors
	inst(1:2) sync_connectors of SMA_CON is
		sync_connectors(1).refDes = "P28";
		sync_connectors(2).refDes = "P27";

		sync_connectors(1).sig = SYNC_OUT;
		sync_connectors(2).sig = SYNC_IN;
		gnd = <gnd>;
	end inst;
	
	// a sync termination resitor
	inst sync_termination of res_0603 is
		value = "10K";
		refDes = "R9";

		a = SYNC_IN;
		b = gnd;
	end inst;
	
	/****************************************************************************************
	 * End of schematic page 3, beginning of schematic page 4-5 from original source
	 */
	 
	// a massive surface mount connector array
	inst docking_connector of asp_134488_01 is
		refDes = "P64";
		
		// data ports 1 through 5
		a[2] = DP1_M2C_P;
		a[3] = DP1_M2C_N;
		a[6] = DP2_M2C_P;
		a[7] = DP2_M2C_N;
		a[10] = DP3_M2C_P;
		a[11] = DP3_M2C_N;
		a[14] = DP4_M2C_P;
		a[15] = DP4_M2C_N;
		a[18] = DP5_M2C_P;
		a[19] = DP5_M2C_N;
		a[22] = DP1_C2M_P;
		a[23] = DP1_C2M_N;
		a[26] = DP2_C2M_P;
		a[27] = DP2_C2M_N;
		a[30] = DP3_C2M_P;
		a[31] = DP3_C2M_N;
		a[34] = DP4_C2M_P;
		a[35] = DP4_C2M_N;
		a[38] = DP5_C2M_P;
		a[39] = DP5_C2M_N;
		
		// gigabit transceiver clock 1 m2C
		b[20] = GBTCLK1_M2C_P;
		b[21] = GBTCLK1_M2C_N;
		b[1,4,5,8,9,12,13,16,17,24,25,28,29,32,33,36,37,40] = open;
		
		// data port 0 and sync signals
		c[2] = DP0_C2M_P;
		c[3] = DP0_C2M_N;
		c[6] = DP0_M2C_P;
		c[7] = DP0_M2C_N;
		c[10] = SYNC_OUT;
		c[11] = SYNC_IN;
		c[14] = DISCRETE[0];
		c[15] = DISCRETE[1];
		c[18] = DISCRETE[2];
		c[19] = DISCRETE[3];
		c[22] = DISCRETE[4];
		c[23] = DISCRETE[5];
		c[26] = DISCRETE[6];
		c[27] = DISCRETE[7];
		c[39] = +3V3;
		c[30,31,34,35,37] = open;
		
		// gigabit transceiver clock 0 m2c
		d[1] = GBTCLK0_M2C_P;
		d[4] = GBTCLK0_M2C_N;
		d[30] = $4N126;
		d[31] = $4N126;
		d[36] = +3V3;
		d[38] = +3V3;
		d[40] = +3V3;
		d[5,8,9,11,12,14,15,17,18,20,21,23,24,26,27,29,32,33,34,35] = open;
		
		// opens
		e[2,3,6,7,9,10,12,13,15,16,18,19,21,22,24,25,27,28,30,31,33,34,36,37,39] = open;
		f[1,4,5,7,8,10,11,13,14,16,17,19,20,22,23,25,26,28,29,31,32,34,35,37,38,40] = open;
		g[2,3,6,7,9,10,12,13,15,16,18,19,21,22,24,25,27,28,30,31,33,34,36,37,39] = open;
		h[1,2,4,5,7,8,10,11,13,14,16,17,19,20,22,23,25,26,28,29,31,32,34,35,37,38,40] = open;
		j[2,3,6,7,9,10,12,13,15,16,18,19,21,22,24,25,27,28,30,31,33,34,36,37,39] = open;
		k[1,4,5,7,8,10,11,13,14,16,17,19,20,22,23,25,26,28,29,31,32,34,35,37,38,40] = open;
		
		// grounds
		a[1,4,5,8,9,12,13,16,17,20,21,24,25,28,29,32,33,36] = <gnd>;
		b[2,3,6,7,10,11,14,15,18,19,22,23,26,27,30,31,34,35,38,39] = <gnd>;
		c[1,4,5,8,9,12,13,16,17,20,21,24,25,28,29,32,33,36] = <gnd>;
		d[2,3,6,7,10,13,16,19,22,25,28,37,39] = <gnd>;
		e[1,4,5,8,11,14,19,20,23,26,29,32,35,38,40] = <gnd>;
		f[2,3,6,9,12,15,18,21,24,28,30,33,36,39] = <gnd>;
		g[1,4,5,8,11,14,17,20,23,26,29,32,35,38,40] = <gnd>;
		h[3,6,9,12,15,18,21,24,27,30,33,36,39] = <gnd>;
		j[1,4,5,8,11,14,17,20,23,26,29,32,35,38,40] = <gnd>;
		k[2,3,6,9,12,15,18,21,24,27,30,33,36,39] = <gnd>;

	end inst;
	
	// the I/O header
	inst header_IO of hdr_2x8 is
		refDes = "J2";

		sig = DISCRETE;
		gnd = <gnd>;
	end inst;
	
end;   