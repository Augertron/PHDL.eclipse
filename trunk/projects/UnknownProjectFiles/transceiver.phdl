/**
 * Design: transceiver.phdl
 * Adapted from PADS fmc_module.prj
 * Submitted by: Pete Dudley, Sandia Labs
 * Translated by: Brad Riching, Brigham Young University
 *
 * Dependencies: fmc_module_top.phdl
 *
 * Notes:
 * This is an exact translation of the schematic, page 2 of the design, for a typical 
 * port.  Pay particular attention to how easy it is to define and instance a sub-design.
 * If this design is compiled by itself, it will generate an error that there is no
 * top-level design found.  This is because the transceiver has four ports declared
 * below.  As such, this design must be compiled alongside a top-level design that
 * instances it and accesses these ports, such as fmc_module_top.phdl.
 */

design transceiver is

	// the ports which make these "nets" accessible to higher level designs
	port pin tx_p;
	port pin tx_n;
	port pin rx_p;
	port pin rx_n;

	// an SMA connector device declaration
	device SMA_CON is
		refprefix = "P";
		name = "";
		value = "";
		pkg_type = "SMA_STRAIGHT";
		mfgr = "MOLEX";
		partNumber = "73391-0070";
		cost = "4.97";
	begin
		pin sig = "1";
		pin[3:0] gnd = "5,4,3,2";
	end;
	
	// a non-polarized surface mount capacitor device declaration
	device cap_0402 is
		refPrefix = "C";
		name = "";
		value = "";
		pkg_type = "CC0402";
		mfgr = "PANASONIC";
		partNumber = "ECJ-0EB1A104K";
		cost = "0.03";
	begin
		pin a = "1";
		pin b = "2";
	end;
	
	// the only two nets needed
	net sma_rx_p;
	net sma_rx_n;
	
begin

	// an array of four sma connectors for the transceiver port
	inst port_transceiver : SMA_CON[1:4] is
		refDes(1) = "P9";
		refDes(2) = "P10";
		refDes(3) = "P16";
		refDes(4) = "P15";
	begin
		sig(0) = tx_p;
		sig(1) = tx_n;
		sig(2) = sma_rx_p;
		sig(3) = sma_rx_n;
		gnd = gnd;
	end;
	
	// the two coupling caps needed on the receiver of a port
	inst rx_coupling_caps : cap_0402[1:2] is
		// no refDes constraints.  It doesn't really make sense to
		// constrain them when a sub-design is to be instanced later.
	begin
		// these are examples of concatenation.
		// sma_rx_p goes to a(1) and sma_rx_n goes to a(2), and similar for b.
		
		a = sma_rx_p & sma_rx_n;
		b = rx_p & rx_n;
		
		// This could also be written:
		// a(1) = sma_rx_p;
		// a(2) = sma_rx_n;
		// b(1) = rx_p;
		// b(2) = rx_n;
	end;

end;
