/*
 *	Heart Rate Monitor
 *	Found on CPU Ready Consulting, inc.
 *	Coded by Richard Black
 *	Date: July 27, 2011
 */
 
design heartMon is

	include "devices.phdl";
	
	net 5v, 9v, gnd, vcc, 9V_ANA, agnd;
	net PROBE1, PROBE2;
	net HRT_BEAT;
	net MISO;
	net SCK;
	net MOSI;
	net RST;
	net SW;
	net PWR_ENA;
	net DIG1, DIG2, DIG3, DIG4;
	net SEGA, SEGB, SEGC, SEGD, SEGE, SEGF, SEGG, SEGDP;
	net SA, SB, SC, SD, SE, SF, SG, SDP;
	net[3:6] bjt_b;
	net[2:1] ca1, ca2;
	net[1:2] power_bjt_b;
	net power_bjt_c;
	net adj;
	net x1a;
	net filter_node;
	net cpu_res;
	net[1:4] op_pos;
	net[1:4] op_neg;
	net[1:4] op_out;
	net inter_res;
	net[1:3] rc_node;
	net large_rc_node;

begin

	// ******************* Page 1 *******************
	
	inst switch of SPST is
		a = SW;
		b = open;
		c = gnd;
		d = open;
	end inst;
	
	inst volt_clip of 9V-CLIP is
		pos = 9v;
		neg = SW;
	end inst;
	
	inst(1:2) power_bjt of BJT is
		combine(1).c = gnd;
		combine(1).e = SW;
		combine(1).b = power_bjt_b[1];
		
		combine(2).c = power_bjt_c;
		combine(2).e = 5v;
		combine(2).b = power_bjt_b[2];
		each(2).partNum = "2N3906D26ZCT-ND";
	end inst;
	
	inst(1:7) power_res_array of Resistor is
		combine(1).a = power_bjt_b[1];
		combine(1).b = SW;
		
		combine(2).a = power_bjt_b[1];
		combine(2).b = power_bjt_c;
		
		combine(3).a = 5v;
		combine(3).b = power_bjt_b[2];
		
		combine(4).a = power_bjt_b[2];
		combine(4).b = PWR_ENA;
		
		combine(5).a = vcc;
		combine(5).b = adj;
		
		combine(6).a = adj;
		combine(6).b = gnd;
		
		combine(7).a = gnd;
		combine(7).b = agnd;
	end inst;
	
	inst(1:4) power_caps of Capacitor is
		combine(1).a = gnd;
		combine(1).b = SW;
		
		combine(2).a = power_bjt_b[1];
		combine(2).b = SW;
		
		combine(3).a = 9v;
		combine(3).b = gnd;
		
		combine(4).a = vcc;
		combine(4).b = gnd;
	end inst;
	
	inst power_supply of LM317LDR2 is
		vin = 9v;
		nc = open;
		adjust = adj;
		vout = <vcc>;
	end inst;
	
	inst CPU of ATTINY2313-20SU is
		vcc = 5v;
		reset = RST;
		xtal[1] = SEGDP;
		xtal[2] = x1a;
		PB[0] = HRT_BEAT;
		PB[1] = filter_node;
		PB[2] = SEGF;
		PB[3] = SEGG;
		PB[4] = DIG1;
		PB[5] = DIG2;
		PB[6] = DIG3;
		PB[7] = DIG4;
		PD[0] = SEGA;
		PD[1] = SEGB;
		PD[2] = SEGC;
		PD[3] = SEGD;
		PD[4] = SEGE;
		PD[5] = cpu_res;
		PD[6] = PWR_ENA;
	end inst;
	
	inst(1:2) cpu_resistors of Resistor is
		combine(1).a = 5v;
		combine(1).b = RST;
		
		combine(2).a = cpu_res;
		combine(2).b = filter_node;
	end inst;
	
	inst(1:4) cpu_caps of Capacitor is
		combine(1).a = 5v;
		combine(1).b = gnd;
		
		combine(2).a = SEGDP;
		combine(2).b = gnd;
		combine(3).a = x1a;
		combine(3).b = gnd;
		each(2:3).value = "18pf";
		
		combine(4).a = filter_node;
		combine(4).b = gnd;
		each(4).value = "10uf";
		each(4).voltage = "25V";
		
	end inst;
	
	inst tranducer of SMT is
		combine.a = x1a;
		combine.b = SEGDP;
	end inst;
	
	MOSI = DIG2;
	MISO = DIG3;
	SCK = DIG4;
	
	// ******************* Page 2 *******************
	inst(1:8) res_seg_array of Resistor is
		value = "120";
		combine.a = SEGA & SEGB & SEGC & SEGD & SEGE & SEGF & SEGG & SEGDP;
		combine.b = SA & SB & SC & SD & SE & SF & SG & SDP;
		partNum = "S120QCT-ND";
	end inst;

	inst(1:4) res_bjt_array of Resistor is
		value = "1K";
		tolerance = "5%";
		partNum = "CF14JT1K00CT-ND";
		combine.a = DIG1 & DIG2 & DIG3 & DIG4;
		combine.b = bjt_b;
	end inst;

	inst(1:4) bjt_seg_array of BJT is
		combine.b = bjt_b;
		combine.c = <5v>;
		combine.e = ca2[2] & ca1[2] & ca2[1] & ca1[1];
	end inst;
	
	inst(1:2) displays of DISP_A-562G is
		A = <SA>;
		B = <SB>;
		C = <SC>;
		D = <SD>;
		E = <SE>;
		F = <SF>;
		G = <SG>;
		DP = <SDP>;
		combine.DIG = ca1[1] & ca2[1] & ca1[2] & ca2[2];
	end inst;
	
	inst(1:4) test_points of TEST_POINT is
		combine.a = PROBE1 & PROBE2 & agnd & gnd;
	end inst;
	
	// ******************* Page 3 *******************
	inst opAmps of LM324DR is
		vcc = 9V_ANA;
		gnd = agnd;
		pos = op_pos;
		neg = op_neg;
		out = op_out;
	end inst;
	
	inst(1:13) caps of Capacitor is
		combine(1:2).a = <9V_ANA>;
		combine(1:2).b = <agnd>;
		each(1:2).value = "10uF";
		each(1:2).voltage = "25V";
		each(1:2).cost = "0.95";
		
		combine(3:4).a = op_pos[1:2];
		combine(3:4).b = PROBE1 & PROBE2;
		each(3:4).value = ".1uF";
		each(3:4).voltage = "50V";
		
		combine(5).a = large_rc_node;
		combine(5).b = agnd;
		each(5).value = "10uF";
		each(5).voltage = "25V";
		each(5).cost = "0.95";
		
		combine(6).a = rc_node[3];
		combine(6).b = op_neg[2];
		each(6).value = "1uF";
		each(6).voltage = "15V";
		each(6).cost = "0.24";
		
		combine(7).a = op_neg[2];
		combine(7).b = op_out[2];
		each(7).value = "470pF";
		each(7).voltage = "50V";
		each(7).cost = "unknown";
		
		combine(8).a = inter_res;
		combine(8).b = op_neg[3];
		combine(9).a = op_pos[3];
		combine(9).b = agnd;
		each(8:9).value = ".1uF";
		each(8:9).voltage = "50V";
		
		combine(10).a = op_pos[4];
		combine(10).b = agnd;
		combine(11).a = rc_node[1];
		combine(11).b = agnd;
		each(10:11).value = ".1uF";
		each(10:11).voltage = "50V";
		
		combine(12).a = rc_node[2];
		combine(12).b = agnd;
		each(12).value = "10uF";
		each(12).voltage = "25V";
		each(12).cost = "0.95";
		
		combine(13).a = op_out[4];
		combine(13).b = HRT_BEAT;
		each(13).value = "10uF";
		each(13).voltage = "25V";
		each(13).cost = "0.95";
	end inst;
	
	inst(1:14) amp_res_array of Resistor is
		combine(1).a = 9v;
		combine(1).b = 9V_ANA;
		each(1).value = "0";
		each(1).partNum = "CD14ZT0R00CT-ND";
		
		combine(2).a = op_pos[2];
		combine(3).a = op_pos[1];
		combine(2:3).b = <large_rc_node>;
		each(2:3).value = "10M";
		each(2:3).tolerance = "5%";
		each(2:3).partNum = "CF14JT10M0CT-ND";
		
		combine(4:5).a = <large_rc_node>;
		combine(4).b = 9V_ANA;
		combine(5).b = agnd;
		each(4:5).value = "10K";
		each(4:5).tolerance = "5%";
		
		combine(6).a = op_out[1];
		combine(6).b = rc_node[3];
		each(6).value = "10K";
		each(6).tolerance = "5%";
		
		combine(7).a = op_out[2];
		combine(7).b = op_neg[2];
		each(7).value = "10M";
		each(7).tolerance = "5%";
		each(7).partNum = "CF14JT10M0CT-ND";
		
		combine(8).a = op_out[2];
		combine(8).b = inter_res;
		each(8).value = "100K";
		each(8).tolerance = "5%";
		each(8).partNum = "CF14JT100KCT-ND";
		
		combine(9).a = inter_res;
		combine(9).b = op_pos[3];
		each(9).value = "100K";
		each(9).tolerance = "5%";
		each(9).partNum = "CF14JT100KCT-ND";
		
		combine(10).a = op_out[3];
		combine(10).b = op_pos[4];
		each(10).value = "100K";
		each(10).tolerance = "5%";
		each(10).partNum = "CF14JT100KCT-ND";
		
		combine(11).a = op_pos[4];
		combine(11).b = rc_node[1];
		each(11).value = "100K";
		each(11).tolerance = "5%";
		each(11).partNum = "CF14JT100KCT-ND";
		
		combine(12).a = rc_node[2];
		combine(12).b = op_neg[4];
		each(12).value = "4.7K";
		each(12).tolerance = "5%";
		each(12).partNum = "CF14JT4K70CT-ND";
		
		combine(13).a = op_neg[4];
		combine(13).b = op_out[4];
		each(13).value = "100K";
		each(13).tolerance = "5%";
		each(13).partNum = "CF14JT100KCT-ND";
		
		combine(14).a = op_out[4];
		combine(14).b = agnd;
		each(14).value = "10K";
		each(14).tolerance = "5%";
	end inst;
	
	op_neg[3] = op_out[3];
	op_neg[1] = op_out[1];
	
end design;